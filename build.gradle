apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'appengine'

// Google App Engine Standard Environment supports Java 7 only (as from August 2016, https://cloud.google.com/appengine/docs)
sourceCompatibility = 1.7 // Java version compatibility to use when compiling Java source.
targetCompatibility = 1.7 // Java version to generate classes for.

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.appengine:gradle-appengine-plugin:+'
    }
}

// check if JAVA_HOME is set, otherwise build tasks will fail
gradle.taskGraph.whenReady {
    graph ->
    if (System.env.'JAVA_HOME' == null) {
        throw new GradleException("JAVA_HOME not set. Please set it, otherwise this task can not be executed.")
    }
    else {
        println "JAVA_HOME = " + System.env.'JAVA_HOME'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    appengineSdk 'com.google.appengine:appengine-java-sdk:+'

    compile 'com.google.appengine:appengine-api-1.0-sdk:+'
    compile 'com.google.appengine.tools:appengine-gcs-client:+'
    compile 'com.googlecode.objectify:objectify:+'
    compile 'javax.servlet:servlet-api:2.5' // GAE works with only with 2.5
    compile 'commons-fileupload:commons-fileupload:+'

    // testing dependencies
    testCompile 'com.google.appengine:appengine-testing:+'
    testCompile 'com.google.appengine:appengine-api-stubs:+'
    testCompile 'com.google.appengine:appengine-api-labs:+'
    testCompile 'com.google.appengine:appengine-tools-sdk:+'
    testCompile 'junit:junit:4.+'
    testCompile 'org.mockito:mockito-core:2.11.0'
}

test {
    //if we test the TestSuites then we can get IllegalArgumentExceptions because some test is run twice
    //-todo make the test durable with more than 1 testrun
    exclude '**/*TestSuite.class'
}

appengine {
    httpPort = 8080
    downloadSdk = true
    jvmFlags = ['-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=8000']

    appcfg {
        oauth2 = true
        noCookies = false
    }
}
